buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.maxhenkel.de/repository/public' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'de.maxhenkel.forge-update', name: 'forge-update', version: '1.0.2'
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '4.0.4'
    }
}

plugins {
    // https://github.com/matthewprenger/CurseGradle
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'forge-update'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

version = mod_version
group = mod_packagename
archivesBaseName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: mod_minecraft_version

    runs {
        client {
            workingDirectory project.file('run').canonicalPath
            property 'forge.logging.console.level', 'debug'
            args = ['--username', 'henkelmax']
            source sourceSets.main
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server').canonicalPath
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${mod_minecraft_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${theoneprobe_mc_version}:${theoneprobe_mc_version}-${theoneprobe_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${theoneprobe_mc_version}:${theoneprobe_mc_version}-${theoneprobe_version}")

    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:additions")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:tools")
    shadow fg.deobf("mekanism:Mekanism:${mekanism_version}:api")

    shadow fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}:api")
    runtimeOnly fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}")
    runtimeOnly fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}:javadoc")

    compile fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}")

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    manifest {
        attributes(["Specification-Title"     : mod_name,
                    "Specification-Vendor"    : mod_vendor,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : mod_name,
                    "Implementation-Version"  : mod_version,
                    "Implementation-Vendor"   : mod_vendor,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.toml'

        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'mod_vendor': mod_vendor,
                'mod_name': mod_name,
                'mod_forge_version': mod_forge_version,
                'mod_loader_version': mod_loader_version,
                'mod_minecraft_version': mod_minecraft_version,
                'mod_mekanism_version_range': mod_mekanism_version_range
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.toml'
    }
}

curseforge {
    def apiKeyFile = file('curseforge_api_key.txt')
    if (System.env.CURSEFORGE_API_KEY != null) {
        apiKey = System.env.CURSEFORGE_API_KEY;
    } else if (apiKeyFile.exists()) {
        apiKey = apiKeyFile.text
    } else {
        apiKey = ""
    }
    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = curse_release_type
        addGameVersion mod_minecraft_version
        mainArtifact(jar) {
            displayName = "[${mod_minecraft_version}] ${mod_name} ${mod_version}"
        }
    }
}

forgeUpdate {
    def messages = []
    file('changelog.md').eachLine { String line ->
        if (line.trim().startsWith('-')) {
            messages.add(line.replaceFirst('-', '').trim())
        }
    }
    def apiKeyFile = file('forge_update_api_key.txt')
    if (System.env.FORGE_UPDATE_API_KEY != null) {
        apiKey = System.env.FORGE_UPDATE_API_KEY;
    } else if (apiKeyFile.exists()) {
        apiKey = apiKeyFile.text
    } else {
        apiKey = ""
    }
    serverURL = 'https://update.maxhenkel.de/'
    modID = mod_id
    gameVersion = mod_minecraft_version
    modVersion = mod_version
    updateMessages = messages
    releaseType = curse_release_type
    tags = recommended == 'true' ? ['recommended'] : []
}

repositories {
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "Hwyla"
        url = "https://maven.tehnut.info"
    }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
    maven {
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    mavenLocal()
}

javadoc {
    failOnError = false
}

task deobfJar(type: Jar) {
    classifier = 'deobf'
    from sourceSets.main.output
    manifest {
        attributes(["Specification-Title"     : mod_name,
                    "Specification-Vendor"    : mod_vendor,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : mod_name,
                    "Implementation-Version"  : mod_version,
                    "Implementation-Vendor"   : mod_vendor,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

jar.finalizedBy('reobfJar')

shadowJar {
    project.configurations.shadow.setTransitive(true);
    configurations = [project.configurations.shadow]
    classifier = ""
    relocate 'de.maxhenkel.corelib', "de.maxhenkel.${mod_id}.corelib"
}

reobf {
    shadowJar {}
}
